block = inline+

(* inlines *)
<inline>        = str | space | symbol-inline | symbol
<symbol-inline> = bold | italic | highlight |
                  roam-render |
                  latex | ref | link | tag |
                  image | alias

(* text *)
<str>    = normal-chars
<space>  = space-chars
<symbol> = !symbol-inline symbol-char

(* alias *)
alias           = alias-content
image           = <'!'> alias-content
<alias-content> = label target
label           = <'['> (!']' !alias inline)* <']'>
target          = <'('> (link | !link #'[^\(\)\s]+') <')'>

(* styles *)
bold      = <'**'> (!'**' inline)+ <'**'>
italic    = <'__'> (!'__' inline)+ <'__'>
highlight = <'^^'> (!'^^' inline)+ <'^^'>
code      =  <'`'> #'[^`]+' <'`'>

(* other *)
link        = <'[['> (link | !link #'((?!(\[\[|\]\])).)+')+ <']]'>
ref         = <'(('> #'[a-zA-Z0-9]*' <'))'>
roam-render = <'{{'> (roam-render | !roam-render #'((?!(\{\{|\}\})).)+')+ <'}}'>
latex       = <'$$'> #'((?!\$\$).)+' <'$$'>
tag         = <'#'> (link | !link #'[a-zA-Z0-9@\\/_\-]+')

(* characters *)
<symbol-char>    = #'[*_^`|\[\]!{}$()#]'
<normal-chars>   = #'[^*_^`|\[\]!{}$()\s#]*'
<space-chars>    = #'\s+'
